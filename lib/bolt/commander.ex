defmodule Bolt.Commander do
  alias Bolt.Commander.Server
  alias Nostrum.Api

  defp find_failing_predicate(msg, predicates) do
    predicates
    |> Enum.map(& &1.(msg))
    |> Enum.find(&match?({:error, _embed}, &1))
  end

  @spec invoke(Map.t(), Nostrum.Structs.Message.t(), [String.t()]) :: no_return()
  defp invoke(%{callback: callback, parser: parser} = command, msg, args) do
    case Map.get(command, :predicates, []) do
      # no predicates -> invoke command directly
      [] ->
        # TODO: Instead of splitting the entire message content,
        #       only split off the actual command, since that is
        #       not of interest to the command handler.
        callback.(msg, parser.(Enum.join(args, " ")))

      # non-empty predicate list -> ensure all of them pass
      predicates ->
        failed = find_failing_predicate(msg, predicates)

        case failed do
          nil ->
            # all predicates passed. invoke the command
            callback.(msg, parser.(Enum.join(args, " ")))

          {:error, embed} ->
            # a predicate failed. show the response generated by it
            {:ok, _msg} = Api.create_message(msg.channel_id, embed: embed)
        end
    end
  end

  @doc """
  Handle a message sent over the gateway.
  If the message starts with the prefix and
  contains a valid command, the arguments
  are parsed accordingly and passed to
  the command along with the message.
  Otherwise, the message is ignored.
  """
  @spec handle_message(Nostrum.Struct.Message.t()) :: no_return
  def handle_message(msg) do
    with ["." <> command_name | args] <- String.split(msg.content),
         command_map when command_map != nil <- Server.lookup(command_name) do
      invoke(command_map, msg, args)
    else
      _err -> :ignored
    end
  end
end
